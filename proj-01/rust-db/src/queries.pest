// character sets
alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
non_zero_digit = _{ '1'..'0' }
ident_char = _{ alpha | digit | "_" }
path_char = _{ !NEWLINE ~ ANY }

// choice sets
comp_op = { "=" | "!=" | "<=" | "<" | ">=" | ">" }
decl_type = { "Int" | "Float" | "Bool" | "String" }
key_type_def = { int | quoted_string }
any_type_def = { numeric | bool | quoted_string }

// type definitions
numeric = { "-"? ~ (digit+ ~ ".")? ~ digit+ }
int = { "-"? ~ digit+}
bool = { "true" | "false" }
string = { (!"\"" ~ ANY)* }
quoted_string = { "\"" ~ string ~ "\"" }
positive_int = { "0"* ~ non_zero_digit ~ digit* }
path = { path_char+ }

// comparison tree
comparison = { ident ~ " " ~ comp_op ~ " " ~ any_type_def }
comparison_braced = { comparison | "(" ~ comparison_or ~ ")" }
comparison_and = { comparison_braced ~ (" AND " ~ comparison_and)? }
comparison_or = { comparison_and ~ (" OR " ~ comparison_or)? }

// identifiers, declarations and assignments
ident = { !digit ~ !"_" ~ ident_char+ }
ident_list = { ident ~ ( ", " ~ ident )* }
decl = { ident ~ ": " ~ decl_type }
decl_list = { decl ~ ( ", " ~ decl )* }
assign = { ident ~ " = " ~ any_type_def }
assign_list = { assign ~ ( ", " ~ assign )* }

// basic clauses for SELECT
select_clause = { "SELECT " ~ ident_list }
from_clause = { " FROM " ~ ident }
where_clause = { " WHERE " ~ comparison_or }
order_clause = { " ORDER_BY " ~ ident_list }
limit_clause = { " LIMIT " ~ positive_int }

// database queries
select_query = { select_clause ~ from_clause ~ where_clause? ~ order_clause? ~ limit_clause? }
create_query = { "CREATE " ~ ident ~ " KEY " ~ ident ~ (NEWLINE+ ~ "FIELDS " | " FIELDS ") ~ decl_list }
insert_query = { "INSERT " ~ assign_list ~ " INTO " ~ ident }
delete_query = { "DELETE " ~ key_type_def ~ " FROM " ~ ident }
save_query = { "SAVE_AS " ~ path }
read_query = { "READ_FROM " ~ path }

// main command
command = { select_query | create_query | insert_query | delete_query | save_query | read_query }
command_list = { command ~ (NEWLINE+ ~ command)* }